/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'java-library'

description = 'Solr Integration with Tika for extracting content from binary file formats such as Microsoft Word and Adobe PDF'

ext {
  // Disable security manager for extraction module tests
  useSecurityManager = false
}

configurations.configureEach {
  // Exclude SpotBugs Annotations, a `compileOnlyApi` dependency of Log4j.
  //
  // Why:
  // - Log4j includes them only to silence harmless `-Xlint:classfile` warnings
  //   when consumers reference classes annotated with `@SuppressFBWarnings`.
  //   Without the jar, compilation may emit warnings (and fail under `-Werror`).
  //   See: https://github.com/apache/logging-log4j2/issues/3110#issuecomment-2423586754
  //
  // - However, SpotBugs Annotations are LGPL-2.1 licensed. Including them,
  //   even at compile time, breaks our license compatibility checks.
  //
  // Net result: unless compiling with `-Werror`, it is safe to exclude the jar.
  exclude group: "com.github.spotbugs", module: "spotbugs-annotations"
}

dependencies {
  implementation platform(project(':platform'))
  implementation project(':solr:core')
  implementation project(':solr:solrj')

  implementation libs.apache.lucene.core
  implementation libs.slf4j.api

  // Tika core - needed for common interfaces and utilities
  implementation libs.apache.tika.core

  // For 'tikaserver' backend
  implementation libs.eclipse.jetty.client
  permitUsedUndeclared libs.eclipse.jetty.http
  permitUsedUndeclared libs.eclipse.jetty.util
  permitUsedUndeclared libs.eclipse.jetty.io

  testImplementation project(':solr:test-framework')
  testImplementation libs.apache.lucene.testframework
  testImplementation libs.junit.junit
  testImplementation libs.testcontainers
  testImplementation libs.carrotsearch.randomizedtesting.runner
}
