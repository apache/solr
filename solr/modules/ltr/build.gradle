/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'java-library'

description = 'Learning to Rank Package'

ext {
  pythonClientCopyDir = layout.projectDirectory.dir("example/solrclient")
}

configurations {
  generatedPythonClient
  localPythonClientCopy
}

dependencies {
  implementation project(':solr:core')
  implementation project(':solr:solrj')

  implementation libs.apache.lucene.core

  implementation libs.slf4j.api

  // Used by example scripts
  generatedPythonClient project(path: ":solr:api", configuration: "pythonClient")
  localPythonClientCopy files(pythonClientCopyDir) {
    builtBy "copyPythonClientToExample"
  }

  testImplementation(libs.mockito.core, {
    exclude group: "net.bytebuddy", module: "byte-buddy-agent"
  })
  testRuntimeOnly(libs.mockito.subclass, {
    exclude group: "net.bytebuddy", module: "byte-buddy-agent"
  })

  testImplementation project(':solr:test-framework')
  testImplementation libs.apache.lucene.testframework
  testImplementation libs.carrotsearch.randomizedtesting.runner
  testImplementation libs.junit.junit
  testImplementation libs.hamcrest.hamcrest

  testImplementation libs.commonsio.commonsio
}

task copyPythonClientToExample(type: Sync) {
  group = "Solr Python Client"
  from configurations.generatedPythonClient
  into project.pythonClientCopyDir
}
