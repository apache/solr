/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'java-library'

description = 'HDFS Contrib Module'

dependencies {
  configurations.all {
    exclude group: 'log4j', module: 'log4j'
    exclude group: 'commons-logging', module: 'commons-logging'
    exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    exclude group: 'org.apache.yetus', module: 'audience-annotations'
    exclude group: 'org.codehaus.mojo', module: 'animal-sniffer-annotations'
   // be conservative on what's added here.  Affects *all* configs, including internal ones.
  }

  implementation project(':solr:core')

  // Hadoop dependencies
  implementation ('org.apache.hadoop:hadoop-annotations') { transitive = false }
  implementation ('org.apache.hadoop:hadoop-auth') { transitive = false }
  implementation ('org.apache.hadoop:hadoop-common') { transitive = false }
  implementation ('org.apache.hadoop:hadoop-hdfs-client') { transitive = false }
  implementation ('org.apache.hadoop:hadoop-hdfs') { transitive = false }

  // Guava implements the VisibleForTesting annotations
  implementation ('com.google.guava:guava') { transitive = false }

  // Caffeine cache to implement HDFS block caching  
  implementation ('com.github.ben-manes.caffeine:caffeine')

  // Many HDFS tests are using/subclassing test framework classes
  testImplementation project(':solr:test-framework')

  // HdfsCheckindexTool tests reference LuceneTestCase
  testImplementation ('org.apache.lucene:lucene-test-framework')

  // hadoop dependencies for tests
  testImplementation ('org.apache.hadoop:hadoop-hdfs') { transitive = false }
  testImplementation ('org.apache.hadoop:hadoop-common::tests') { transitive = false }
  testImplementation ('org.apache.hadoop:hadoop-hdfs::tests') { transitive = false }
  testImplementation ('org.apache.hadoop:hadoop-minikdc') { transitive = false }

  testImplementation 'org.apache.logging.log4j:log4j-1.2-api'

  // classes like solr.ICUCollationField, used by NNFailoverTest for example.
  testImplementation project(':solr:modules:analysis-extras')

  // required for instantiating a Zookeeper server in tests or embedded
  runtimeOnly ('org.xerial.snappy:snappy-java')

  // commons packages needed by test classes
  testImplementation('commons-io:commons-io') { transitive = false }

  // Zookeeper dependency - some tests like HdfsCloudBackupRestore need this
  implementation ('org.apache.zookeeper:zookeeper')
}
