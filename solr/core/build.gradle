/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


apply plugin: 'java-library'

description = 'Apache Solr Core'

configurations.all {
  exclude group: 'log4j', module: 'log4j'
  exclude group: 'commons-logging', module: 'commons-logging'
  exclude group: 'org.slf4j', module: 'slf4j-log4j12'
  exclude group: 'org.apache.yetus', module: 'audience-annotations'
  exclude group: 'org.codehaus.mojo', module: 'animal-sniffer-annotations'
  exclude group: "com.google.code.findbugs", module: "jsr305"
  // be conservative on what's added here.  Affects *all* configs, including internal ones.
}

dependencies {
  // Export these dependencies so that they're imported transitively by
  // other modules.

  // These Lucene modules are the most significant to Solr
  api "org.apache.lucene:lucene-core"
  api "org.apache.lucene:lucene-analysis-common"
  api "org.apache.lucene:lucene-queries"

  // We export logging api with dependencies, which is useful for all modules
  api 'org.slf4j:slf4j-api'

  api project(':solr:solrj')
  api project(':solr:server')

  // Non-API below; although there are exceptions

  implementation "org.apache.lucene:lucene-analysis-kuromoji"
  implementation "org.apache.lucene:lucene-analysis-nori"
  implementation "org.apache.lucene:lucene-analysis-phonetic"
  implementation "org.apache.lucene:lucene-backward-codecs"
  implementation "org.apache.lucene:lucene-codecs"
  implementation "org.apache.lucene:lucene-classification"
  implementation "org.apache.lucene:lucene-expressions"
  implementation "org.apache.lucene:lucene-grouping"
  implementation "org.apache.lucene:lucene-highlighter"
  implementation "org.apache.lucene:lucene-join"
  implementation "org.apache.lucene:lucene-misc"
  implementation "org.apache.lucene:lucene-queryparser"
  implementation "org.apache.lucene:lucene-sandbox"
  implementation "org.apache.lucene:lucene-spatial-extras"
  implementation "org.apache.lucene:lucene-suggest"

  // Collections & lang utilities
  implementation ('com.google.guava:guava') { transitive = false }
  implementation 'org.apache.commons:commons-lang3'
  implementation 'org.apache.commons:commons-math3'
  implementation 'commons-io:commons-io'
  implementation 'com.carrotsearch:hppc'
  implementation 'org.apache.commons:commons-collections4'

  implementation('com.github.ben-manes.caffeine:caffeine') { transitive = false }

  implementation 'commons-codec:commons-codec'

  implementation 'commons-cli:commons-cli'

  implementation 'org.locationtech.spatial4j:spatial4j'

  implementation 'com.fasterxml.jackson.core:jackson-annotations'
  implementation 'com.fasterxml.jackson.core:jackson-core'
  implementation 'com.fasterxml.jackson.core:jackson-databind'
  implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-smile'

  implementation 'org.apache.httpcomponents:httpclient'
  implementation 'org.apache.httpcomponents:httpcore'

  implementation 'javax.servlet:javax.servlet-api'

  implementation 'org.eclipse.jetty:jetty-client'

  implementation 'org.eclipse.jetty.http2:http2-common'
  implementation 'org.eclipse.jetty.http2:http2-server'
  implementation 'org.eclipse.jetty:jetty-alpn-server'
  implementation 'org.eclipse.jetty:jetty-http'
  implementation 'org.eclipse.jetty:jetty-io'
  implementation 'org.eclipse.jetty:jetty-rewrite'
  implementation 'org.eclipse.jetty:jetty-server'
  implementation 'org.eclipse.jetty:jetty-servlet'
  implementation 'org.eclipse.jetty:jetty-util'

  // ZooKeeper & Curator
  implementation('org.apache.zookeeper:zookeeper', {
    exclude group: "org.apache.yetus", module: "audience-annotations"
    exclude group: "log4j", module: "log4j"
    exclude group: "org.slf4j", module: "slf4j-log4j12"
  })
  implementation ('org.apache.zookeeper:zookeeper-jute') {
    exclude group: 'org.apache.yetus', module: 'audience-annotations'
  }
  // required for instantiating a Zookeeper server (for embedding ZK or running tests)
  runtimeOnly ('org.xerial.snappy:snappy-java')
  implementation ('org.apache.curator:curator-client')
  implementation ('org.apache.curator:curator-framework')
  runtimeOnly ('org.apache.curator:curator-recipes')

  // For Package Manager
  implementation 'com.github.zafarkhaja:java-semver'

  implementation('com.jayway.jsonpath:json-path', {
    exclude group: "net.minidev", module: "json-smart"
  })

  // StatsComponents percentiles
  implementation 'com.tdunning:t-digest'

  // Distributed Tracing
  api 'io.opentracing:opentracing-api' // Tracer is exposed on some methods
  implementation 'io.opentracing:opentracing-noop'
  implementation 'io.opentracing:opentracing-util'
  testImplementation 'io.opentracing:opentracing-mock'

  implementation 'org.apache.commons:commons-exec'

  implementation 'org.apache.commons:commons-text'

  implementation 'org.apache.logging.log4j:log4j-api'
  implementation 'org.apache.logging.log4j:log4j-core'
  implementation 'org.apache.logging.log4j:log4j-slf4j-impl'

  implementation 'io.dropwizard.metrics:metrics-core'
  implementation ('io.dropwizard.metrics:metrics-graphite', {
    exclude group: "com.rabbitmq", module: "amqp-client"
  })
  implementation 'io.dropwizard.metrics:metrics-jmx'
  implementation 'io.dropwizard.metrics:metrics-jvm'

  testImplementation 'org.slf4j:jcl-over-slf4j'

  // For faster XML processing than the JDK
  implementation 'org.codehaus.woodstox:stax2-api'
  implementation 'com.fasterxml.woodstox:woodstox-core'

  // SQL Parser via Calcite
  implementation ('org.apache.calcite:calcite-core', {
    exclude group: 'org.apache.calcite.avatica', module: 'avatica-metrics'
    exclude group: 'com.google.guava', module: 'guava' // TODO remove this to keep versions in sync
    exclude group: 'com.fasterxml.jackson.dataformat', module: 'jackson-dataformat-yaml'
    exclude group: 'com.google.uzaygezen', module: 'uzaygezen-core'
    exclude group: 'com.yahoo.datasketches', module: 'sketches-core'
    exclude group: 'org.apache.commons', module: 'commons-dbcp2'
    exclude group: 'net.minidev', module: 'json-smart'
    exclude group: 'net.hydromatic', module: 'aggdesigner-algorithm'
  })
  // sub-deps of calcite-core that we reference directly
  implementation ('org.apache.calcite:calcite-linq4j') { transitive = false }
  implementation ('org.apache.calcite.avatica:avatica-core') { transitive = false }

  // Hadoop auth framework
  implementation ('org.apache.hadoop:hadoop-annotations')
  implementation ('org.apache.hadoop:hadoop-auth') { transitive = false }
  implementation ('org.apache.hadoop:hadoop-common') { transitive = false }
  // transitive of hadoop-common; used by Kerberos auth
  runtimeOnly 'commons-collections:commons-collections'
  runtimeOnly 'com.google.re2j:re2j'
  runtimeOnly 'org.apache.commons:commons-configuration2'
  runtimeOnly 'org.apache.htrace:htrace-core4' // note: removed in Hadoop 3.3.2
  runtimeOnly 'org.apache.kerby:kerb-core'
  runtimeOnly 'org.apache.kerby:kerb-util'

  // Hadoop MiniKdc Dependencies (for Kerberos auth tests)
  testImplementation ('org.apache.hadoop:hadoop-minikdc', {
    exclude group:'org.apache.kerby', module:'kerby-xdr'
    exclude group:'org.apache.kerby', module:'token-provider'
  })

  testImplementation "org.apache.lucene:lucene-analysis-icu"

  testImplementation project(':solr:modules:analysis-extras')

  testImplementation project(':solr:core')
  testImplementation project(':solr:test-framework')

  testImplementation 'org.apache.lucene:lucene-test-framework'

  testImplementation 'com.carrotsearch.randomizedtesting:randomizedtesting-runner'
  testImplementation 'junit:junit'
  testImplementation 'org.hamcrest:hamcrest'

  testImplementation('org.mockito:mockito-core', {
    exclude group: "net.bytebuddy", module: "byte-buddy-agent"
  })
}


