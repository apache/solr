/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


apply plugin: 'java-library'

description = 'Apache Solr Core'

configurations.all {
  exclude group: 'log4j', module: 'log4j'
  exclude group: 'commons-logging', module: 'commons-logging'
  exclude group: 'org.slf4j', module: 'slf4j-log4j12'
  exclude group: 'org.apache.yetus', module: 'audience-annotations'
  exclude group: 'org.codehaus.mojo', module: 'animal-sniffer-annotations'
  // be conservative on what's added here.  Affects *all* configs, including internal ones.
}

dependencies {
  // Export these dependencies so that they're imported transitively by
  // other modules.

  // These Lucene modules are the most significant to Solr
  api "org.apache.lucene:lucene-core"
  api "org.apache.lucene:lucene-analysis-common"
  api "org.apache.lucene:lucene-queries"

  api project(':solr:solrj')
  api project(':solr:server')

  // Non-API below; although there are exceptions

  implementation "org.apache.lucene:lucene-analysis-kuromoji"
  implementation "org.apache.lucene:lucene-analysis-nori"
  implementation "org.apache.lucene:lucene-analysis-phonetic"
  implementation "org.apache.lucene:lucene-backward-codecs"
  implementation "org.apache.lucene:lucene-codecs"
  implementation "org.apache.lucene:lucene-classification"
  implementation "org.apache.lucene:lucene-expressions"
  implementation "org.apache.lucene:lucene-grouping"
  implementation "org.apache.lucene:lucene-highlighter"
  implementation "org.apache.lucene:lucene-join"
  implementation "org.apache.lucene:lucene-misc"
  implementation "org.apache.lucene:lucene-queryparser"
  implementation "org.apache.lucene:lucene-sandbox"
  implementation "org.apache.lucene:lucene-spatial-extras"
  implementation "org.apache.lucene:lucene-suggest"

  // Collections & lang utilities
  implementation ('com.google.guava:guava') { transitive = false }
  implementation 'org.apache.commons:commons-lang3'
  implementation 'org.apache.commons:commons-math3'
  implementation 'commons-io:commons-io'
  implementation 'com.carrotsearch:hppc'
  implementation 'org.apache.commons:commons-collections4'
  runtimeOnly 'commons-collections:commons-collections' // for Hadoop and...?

  implementation('com.github.ben-manes.caffeine:caffeine') { transitive = false }

  implementation 'org.slf4j:slf4j-api'

  implementation 'commons-codec:commons-codec'

  implementation 'commons-cli:commons-cli'

  implementation 'com.fasterxml.jackson.core:jackson-databind'
  implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-smile'

  implementation 'org.eclipse.jetty:jetty-client'

  // ZooKeeper & Curator
  implementation 'org.apache.zookeeper:zookeeper'
  // required for instantiating a Zookeeper server (for embedding ZK or running tests)
  runtimeOnly ('org.xerial.snappy:snappy-java')
  implementation ('org.apache.curator:curator-client')
  implementation ('org.apache.curator:curator-framework')
  runtimeOnly ('org.apache.curator:curator-recipes')

  // For Package Manager
  implementation 'com.github.zafarkhaja:java-semver'

  implementation('com.jayway.jsonpath:json-path', {
    exclude group: "net.minidev", module: "json-smart"
  })

  // StatsComponents percentiles
  implementation 'com.tdunning:t-digest'

  // Distributed Tracing
  api 'io.opentracing:opentracing-api' // Tracer is exposed on some methods
  implementation 'io.opentracing:opentracing-noop'
  implementation 'io.opentracing:opentracing-util'
  testImplementation 'io.opentracing:opentracing-mock'

  implementation 'org.apache.commons:commons-exec'

  implementation 'org.apache.commons:commons-text'

  implementation 'org.apache.logging.log4j:log4j-api'
  implementation 'org.apache.logging.log4j:log4j-core'
  implementation 'org.apache.logging.log4j:log4j-slf4j-impl'

  testImplementation 'org.slf4j:jcl-over-slf4j'

  // JWT Auth plugin
  api 'org.bitbucket.b_c:jose4j'

  // For faster XML processing than the JDK
  implementation (group: 'com.fasterxml.woodstox', name: 'woodstox-core')

  // SQL Parser via Calcite
  implementation ('org.apache.calcite:calcite-core', {
    exclude group: 'org.apache.calcite.avatica', module: 'avatica-metrics'
    exclude group: 'com.fasterxml.jackson.dataformat', module: 'jackson-dataformat-yaml'
    exclude group: 'com.google.uzaygezen', module: 'uzaygezen-core'
    exclude group: 'com.yahoo.datasketches', module: 'sketches-core'
    exclude group: 'org.apache.commons', module: 'commons-dbcp2'
    exclude group: 'net.minidev', module: 'json-smart'
    exclude group: 'net.hydromatic', module: 'aggdesigner-algorithm'
  })
  // sub-deps of calcite-core that we reference directly
  implementation ('org.apache.calcite:calcite-linq4j') { transitive = false }
  implementation ('org.apache.calcite.avatica:avatica-core') { transitive = false }

  // Hadoop; general stuff
  implementation ('org.apache.hadoop:hadoop-common') { transitive = false } // too many to ignore
  implementation ('org.apache.hadoop:hadoop-annotations')
  runtimeOnly 'org.apache.htrace:htrace-core4' // note: removed in Hadoop 3.3.2
  runtimeOnly "org.apache.commons:commons-configuration2"
  testImplementation ('org.apache.hadoop:hadoop-common::tests') { transitive = false }
  // Hadoop Test hacks for Java Security Manager -- SOLR-14033
  testImplementation 'org.apache.commons:commons-compress'
  testImplementation ('com.sun.jersey:jersey-servlet') { transitive = false }

  // HDFS
  implementation ('org.apache.hadoop:hadoop-hdfs-client') { transitive = false }
  testImplementation ('org.apache.hadoop:hadoop-hdfs') { transitive = false }
  testImplementation ('org.apache.hadoop:hadoop-hdfs::tests') { transitive = false }
  testImplementation 'org.apache.logging.log4j:log4j-1.2-api'

  // Hadoop auth framework
  implementation ('org.apache.hadoop:hadoop-auth') { transitive = false }
  runtimeOnly 'com.google.re2j:re2j' // transitive of hadoop-common; used by Kerberos auth
  runtimeOnly ('org.apache.kerby:kerb-core')
  runtimeOnly ('org.apache.kerby:kerb-util')

  // Hadoop MiniKdc Dependencies (for Kerberos auth tests)
  testImplementation ('org.apache.hadoop:hadoop-minikdc', {
    exclude group:'org.apache.kerby', module:'kerby-xdr'
    exclude group:'org.apache.kerby', module:'token-provider'
  })

  testImplementation "org.apache.lucene:lucene-analysis-icu"

  testImplementation project(':solr:contrib:analysis-extras')

  testImplementation project(':solr:test-framework')

  testImplementation('org.mockito:mockito-core', {
    exclude group: "net.bytebuddy", module: "byte-buddy-agent"
  })

  testImplementation('no.nav.security:mock-oauth2-server', {
    exclude group: "ch.qos.logback", module: "logback-core"
    exclude group: "io.netty", module: "netty-all"
    exclude group: "ch.qos.logback", module: "logback-classic"
  })
}


