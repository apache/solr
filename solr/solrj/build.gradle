/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  alias(libs.plugins.openapi.generator)
  alias(libs.plugins.diffplug.spotless)
  id 'java-library'
  id 'java-test-fixtures'
}

description = 'Solrj - Solr Java Client'

dependencies {
  implementation libs.fasterxml.jackson.core.databind
  implementation libs.fasterxml.jackson.core.annotations
  implementation libs.fasterxml.jackson.core.core
  api project(":solr:api")

  implementation libs.slf4j.api
  runtimeOnly libs.slf4j.jcloverslf4j

  compileOnly libs.stephenc.jcip.annotations

  testImplementation project(":solr:solrj-jetty")
  testImplementation libs.eclipse.jetty.client

  testImplementation libs.apache.httpcomponents.httpclient
  testImplementation libs.apache.httpcomponents.httpcore

  testFixturesApi project(':solr:test-framework')
  testImplementation project(':solr:core')

  // ideally ZK centric tests move to solrj-zookeeper but sometimes we depend on ZK here anyway
  testImplementation project(':solr:solrj-zookeeper')
  testImplementation(libs.apache.zookeeper.zookeeper, {
    exclude group: "org.apache.yetus", module: "audience-annotations"
  })
  permitTestUnusedDeclared libs.apache.zookeeper.zookeeper
  testImplementation libs.apache.zookeeper.jute

  testImplementation libs.apache.lucene.core
  testFixturesApi libs.apache.lucene.testframework

  testFixturesApi libs.carrotsearch.randomizedtesting.runner
  testFixturesApi libs.junit.junit
  testFixturesApi libs.hamcrest.hamcrest

  testImplementation libs.commonsio.commonsio
  testImplementation libs.fasterxml.jackson.dataformat.cbor

  testFixturesApi libs.jakarta.servlet.api

  testFixturesApi libs.eclipse.jetty.server
  testFixturesApi libs.eclipse.jetty.ee10.servlet
  testFixturesApi libs.eclipse.jetty.ee10.webapp
  testFixturesRuntimeOnly(libs.eclipse.jetty.alpnjavaserver, {
    exclude group: "org.eclipse.jetty.alpn", module: "alpn-api"
  })
  //testFixturesApi libs.eclipse.jetty.session

  testImplementation(libs.mockito.core, {
    exclude group: "net.bytebuddy", module: "byte-buddy-agent"
  })
  testRuntimeOnly(libs.mockito.subclass, {
    exclude group: "net.bytebuddy", module: "byte-buddy-agent"
  })
  testRuntimeOnly(libs.apache.log4j.slf4j2impl, {
    exclude group: "org.apache.logging.log4j", module: "log4j-api"
  })
  testImplementation libs.fasterxml.jackson.core.core
}

sourceSets {
  testFixtures {
    java {
      srcDir 'src/test-fixtures'
    }
  }
}

compileTestFixturesJava {
  sourceCompatibility = compileTestJava.sourceCompatibility
  targetCompatibility = compileTestJava.targetCompatibility
  options.compilerArgs += ["--release", targetCompatibility]
}

/**
 * Java Code Generation for Solr API
 */

evaluationDependsOn(":solr:api")

configurations {
  openApiSpecFile {
    canBeConsumed = false
  }
}

ext {
  generatedCodeDir = "${buildDir}/generated/"
  javaTemplateDir = "${projectDir}/src/resources/java-template"
}

dependencies {
  openApiSpecFile(project(path: ":solr:api", configuration: "openapiSpec"))
}

/**
 * Code Generation task
 */
openApiGenerate {
  generatorName = "java"
  inputSpec = project(":solr:api").openApiSpecFile

  // Add 'debugModels: ""' or 'debugOperations: ""' to get the JSON input to mustache templating for those components
  globalProperties.set([apis: "", models: "false"])
  templateDir = project.javaTemplateDir
  apiPackage = "org.apache.solr.client.solrj.request"
  outputDir = project.generatedCodeDir
  generateApiTests = false
  generateModelTests = false
  generateApiDocumentation = false
  generateModelDocumentation = false
  cleanupOutput = true
  additionalProperties = ["modelPackage": "org.apache.solr.client.api.model"]
}

tasks.openApiGenerate.dependsOn configurations.openApiSpecFile

def generatedFiles = files("${project.generatedCodeDir}/src/main/java") {
  builtBy tasks.openApiGenerate
}

/**
 * Setup Spotless (Code formatting) for the generated java files
 */
//def generatedExt = new JavaExtension(spotless)
//project.spotlessJavaSetup.execute(generatedExt)
//generatedExt.target(generatedFiles)
//def generatedSpotlessTask = generatedExt.createIndependentApplyTask("generatedSpotless")
//generatedSpotlessTask.group("build")
//generatedSpotlessTask.description("Apply formatting for generated code")
//
//tasks.openApiGenerate.finalizedBy generatedSpotlessTask

/**
 * Add the Generated code to the SolrJ Source paths
 */

sourceSets {
  main {
    java {
      srcDir generatedFiles
    }
  }
}
