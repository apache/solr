/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id 'java'
  id 'war'
}

description = 'Solr webapp'

configurations {
  war {}
  serverLib
  libExt
  solrJettyClientLib
  solrCore
}

dependencies {
  permitUnusedDeclared project(":solr:core")
  libExt project(path: ":solr:server", configuration: "libExt")
  serverLib project(path: ":solr:server", configuration: "serverLib")
  solrCore project(":solr:core")
  solrJettyClientLib project(path: ":solr:core", configuration: "jettyClientImplementation")
  // Make sure we keep the duplicate solrJettyClientLibs, but we want to remove all of the logging and metrics jars already in libExt
  implementation(configurations.solrCore - configurations.serverLib + configurations.solrJettyClientLib - configurations.libExt)
}

war {
  // Why are they in the source code at all if they're excluded from the distribution?
  exclude "libs/angular-cookies.js"
  exclude "libs/angular-route.js"
  exclude "libs/angular-sanitize.js"
  exclude "libs/angular-utf8-base.js"
  exclude "libs/angular.js"
  exclude "libs/chosen.jquery.js"
}

// Expose 'war' archive as an artifact so that it can be packaged in the distribution.
artifacts {
  war tasks.war
}
