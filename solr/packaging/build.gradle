/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import org.apache.tools.ant.util.TeeOutputStream

// This project puts together a "distribution", assembling dependencies from
// various other projects.

plugins {
  id 'base'
  id 'distribution'
}

description = 'Solr distribution packaging'

ext {
  distDir = file("$buildDir/solr-${version}")
  devDir = file("$buildDir/dev")
}

configurations {
  modules
  example
  prometheusExporter
  server
  docs
  docker
  solrTgz
  solrTgzSignature
}

artifacts {
  solrTgz(distTar)
}

dependencies {
  [":solr:modules:analysis-extras",
   ":solr:modules:analytics",
   ":solr:modules:extraction",
   ":solr:modules:clustering",
   ":solr:modules:gcs-repository",
   ":solr:modules:hadoop-auth",
   ":solr:modules:hdfs",
   ":solr:modules:jaegertracer-configurator",
   ":solr:modules:jwt-auth",
   ":solr:modules:langid",
   ":solr:modules:ltr",
   ":solr:modules:s3-repository",
   ":solr:modules:scripting",
   ":solr:modules:sql"
  ].each { moduleName ->
    modules project(path: moduleName, configuration: "packaging")
  }

  prometheusExporter project(path: ":solr:prometheus-exporter", configuration: "packaging")

  example project(path: ":solr:example", configuration: "packaging")
  server project(path: ":solr:server", configuration: "packaging")

  // Copy files from documentation output
  docs project(path: ':solr:documentation', configuration: 'minimalSite')

  docker project(path: ':solr:docker', configuration: 'packaging')

  solrTgzSignature files("$buildDir/distributions/solr-${version}.tgz.asc") {
    builtBy ":solr:distribution:signBinaryTgz"
  }
}

distributions {
  main {
    distributionBaseName = 'solr'
    contents {
      // TODO: This is missing module README files, as compared to 8x

      from(rootDir, {
        include "LICENSE.txt"
        include "NOTICE.txt"
      })

      from(project(":solr").projectDir, {
        include "bin/**"
        include "licenses/**"
        exclude "licenses/README.committers.txt"
        include "CHANGES.txt"
        include "README.md"
      })

      from(configurations.modules, {
        into "modules"
      })

      from(configurations.example, {
        into "example"
      })

      from(configurations.prometheusExporter, {
        into "prometheus-exporter"
      })

      from(configurations.server, {
        into "server"
      })

      from(configurations.docs, {
        into "docs"
      })

      from(configurations.docker, {
        into "docker"
        filesMatching([
            "scripts/**",
        ]) { copy ->
          copy.setMode(0755)
        }
      })

      // Manually correct posix permissions (matters when packaging on Windows).
      filesMatching([
          "**/*.sh",
          "**/bin/post",
          "**/bin/postlogs",
          "**/bin/solr",
          "**/bin/init.d/solr",
          "**/bin/solr-exporter",
      ]) { copy ->
        copy.setMode(0755)
      }

    }
  }
}

installDist {
  into distDir
}

task dev(type: Copy) {
  description "Assemble Solr distribution into 'development' folder at ${devDir}"
  group "build"

  from installDist.outputs
  into devDir
}

distTar {
  compression = Compression.GZIP
}

distZip.enabled = false

assemble.dependsOn installDist

task downloadBats(type: NpmTask) {
  group = 'Build Dependency Download'
  args = ["install", "bats",
                     "ztombol/bats-support",
                     "ztombol/bats-assert",
  ]

  inputs.files("${project.ext.nodeProjectDir}/package.json")
  outputs.dir("${project.ext.nodeProjectDir}/node_modules/bats")
  outputs.dir("${project.ext.nodeProjectDir}/node_modules/bats-support")
  outputs.dir("${project.ext.nodeProjectDir}/node_modules/bats-assert")
}

task integrationTests(type: Exec) {
  dependsOn installDist
  dependsOn downloadBats

  def integrationTestOutput = "$buildDir/test-output"
  def solrHome = "$integrationTestOutput/solr-home"

  inputs.files(fileTree('test'))
  outputs.dir(integrationTestOutput)
  outputs.dir(solrHome)

  doFirst {
    // TODO - if quiet then don't tee
    standardOutput = new TeeOutputStream(System.out, new FileOutputStream("$integrationTestOutput/test-output.txt"))
  }

  environment SOLR_TIP: distDir.toString()
  environment SOLR_HOME: solrHome
  environment SOLR_LOGS_DIR: "$solrHome/logs"
  environment BATS_LIB_PREFIX: "$nodeProjectDir/node_modules"

  executable "$nodeProjectDir/node_modules/bats/bin/bats"

  // TODO allow specifying multiple files?
  args '--print-output-on-failure', 'test/' + propertyOrDefault('bats.tests', '')
}
