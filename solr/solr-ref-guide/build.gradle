/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import org.apache.tools.ant.util.TeeOutputStream

plugins {
    id "com.github.node-gradle.node" version "3.1.1"
}

description = 'Solr Reference Guide'

def buildLocalUI = propertyOrEnvOrDefault("refguide.buildLocalUI", "SOLR_REF_GUIDE_BUILD_LOCAL_UI", "false").toBoolean()

// Attach building the ref guide to standard convention tasks. This
// can be optionally turned off (see SOLR-15670).
if (propertyOrEnvOrDefault('refguide.include', 'SOLR_REF_GUIDE_INCLUDE', "true").toBoolean()) {
    check.dependsOn 'checkSiteLinks'
    assemble.dependsOn 'buildLocalSite'
}

configurations {
    refGuide
    officialPlaybook
    localPlaybook
}

dependencies {
    localPlaybook project(path: ":solr:documentation", configuration: 'javadocs')
    localPlaybook project(path: ":solr:documentation", configuration: 'site')
}

ext {
    antoraVersion = "3.0.1"
    rootNodeDir = "${project.rootDir}/.gradle/node"
    nodeProjectDir = file("${project.ext.rootNodeDir}/ref-guide-project")

    siteDir = "${buildDir}/site"
    antoraConfigBuildDir = "${buildDir}/antora-config"
    playbooksDir = "${project.ext.antoraConfigBuildDir}/playbooks"
    localAntoraYaml = "${project.ext.antoraConfigBuildDir}/antora.yml"
    siteStagingDir = "${buildDir}/site-staging"
    uiBuildDir = "${buildDir}/ui"
    playbookTemplate = "playbook.template.yml"
    localPlaybook = "local-playbook.yml"
    officialPlaybook = "official-playbook.yml"
}

/*
    Generating the Antora and Playbook YAML files
 */
task buildLocalAntoraYaml {
    group = 'Site - Local'
    description "Creates an local antora.yml with all variables populated. Note: This will change the antora.yml in the repo, not a build folder."

    def templateYaml = "antora.yml.template"

    inputs.file(templateYaml)
    outputs.file(project.ext.localAntoraYaml)

    doLast {
        def splitVersion = version.toString().split("\\p{Punct}")
        def props = [
            solr_version       : "${splitVersion[0]}.${splitVersion[1]}.${splitVersion[2]}",
            solr_version_major : splitVersion[0],
            solr_version_minor : splitVersion[1],
            solr_version_patch : splitVersion[2],
        ]
        // Set these dependency versions as lazy gstrings so that they're resolved after evaluation.
        // These variable names must use underscores, not dashes or periods
        props.putAll([
            ["dep_version_commons_codec", "commons-codec", "commons-codec"],
            ["dep_version_dropwizard", "io.dropwizard.metrics", "metrics-core"],
            ["dep_version_hadoop", "org.apache.hadoop", "hadoop-auth"],
            ["dep_version_log4j", "org.apache.logging.log4j", "log4j-core"],
            ["dep_version_opennlp", "org.apache.opennlp", "opennlp-tools"],
            ["dep_version_tika", "org.apache.tika", "tika-core"],
            ["dep_version_zookeeper", "org.apache.zookeeper", "zookeeper"],
            ["dep_version_lucene", "org.apache.lucene", "lucene-core"],
        ].collectEntries { propKey, depGroup, depId -> [propKey, "${-> project.getVersion(depGroup, depId)}"] })

        props.putAll([
            solr_javadocs_link : "https://solr.apache.org/docs/${props.solr_version.replaceAll("\\.", "_")}",
            lucene_javadocs_link :  project(':solr:documentation').luceneDocUrl,
        ])

        copy {
            from(templateYaml) {
                rename {
                    "antora.yml"
                }
            }
            into project.ext.antoraConfigBuildDir

            expand(props)
        }
    }
}

task buildLocalAntoraPlaybookYaml(type: Copy) {
    group = 'Site - Local'
    description "Creates ${project.ext.localPlaybook} to build and test the site locally"

    from(project.ext.playbookTemplate) {
        rename {
            project.ext.localPlaybook
        }
    }
    into project.ext.playbooksDir

    def props = [
        'site_url'           : "",
        'source_url'         : project.rootDir,
        'source_branches'    : "HEAD",
        'start_path'         : project.rootDir.relativePath(file(project.ext.siteStagingDir)),
        'supplemental_files' : "${project.ext.nodeProjectDir}/node_modules/@antora/lunr-extension/supplemental_ui",
        'site_dir'           : file(project.ext.siteStagingDir).relativePath(file(project.ext.siteDir)),
    ]

    expand(props)
    inputs.properties(props)
}

task buildOfficialAntoraPlaybookYaml(type: Copy) {
    group = 'Site - Official'
    description "Creates ${project.ext.officialPlaybook} to build the official Solr ref-guide"

    from(project.ext.playbookTemplate) {
        rename {
            project.ext.officialPlaybook
        }
    }
    into project.ext.playbooksDir

    def props = [
        'site_url'           : "https://solr.apache.org/guide",
        'source_url'         : "https://github.com/apache/solr.git",
        'source_branches'    : ["jira/solr-15556-antora"],
        'start_path'         : 'solr/solr-ref-guide',
        'supplemental_files' : "${project.ext.nodeProjectDir}/node_modules/@antora/lunr-extension/supplemental_ui",
        'site_dir'           : projectDir.relativePath(file(project.ext.siteDir)),
    ]

    expand(props)
    inputs.properties(props)
}

dependencies {
    localPlaybook files("${project.ext.playbooksDir}/${project.ext.localPlaybook}") {
        builtBy tasks.buildLocalAntoraPlaybookYaml
    }
    officialPlaybook files("${project.ext.playbooksDir}/${project.ext.officialPlaybook}") {
        builtBy tasks.buildOfficialAntoraPlaybookYaml
    }
}

/*
    Downloading Build Tools
 */

// We use Node to install and run Antora
node {
    download = true
    version = "16.13.2" // LTS

    // The directory where Node.js is unpacked (when download is true)
    workDir = file("${project.ext.rootNodeDir}/nodejs")

    // The directory where npm is installed (when a specific version is defined)
    npmWorkDir = file("${project.ext.rootNodeDir}/npm")

    // The directory where yarn is installed (when a Yarn task is used)
    yarnWorkDir = file("${project.ext.rootNodeDir}/yarn")

    // The Node.js project directory location
    // This is where the package.json file and node_modules directory are located
    // By default it is at the root of the current project
    nodeProjectDir = project.ext.nodeProjectDir
}

task downloadAntoraCli(type: NpmTask) {
    group = 'Build Dependency Download'
    args = ["install", "@antora/cli@${project.ext.antoraVersion}"]

    inputs.property("Antora version", project.ext.antoraVersion)
    inputs.files("${project.ext.nodeProjectDir}/package.json")
    outputs.dir("${project.ext.nodeProjectDir}/node_modules/@antora/cli")
}

task downloadAntoraSiteGenerator(type: NpmTask) {
    group = 'Build Dependency Download'
    args = ["install", "@antora/site-generator-default@${project.ext.antoraVersion}"]

    inputs.property("Antora version", project.ext.antoraVersion)
    inputs.files("${project.ext.nodeProjectDir}/package.json")
    outputs.dir("${project.ext.nodeProjectDir}/node_modules/@antora/site-generator-default")
}

task downloadAntoraLunrExtension(type: NpmTask) {
    group = 'Build Dependency Download'
    args = ["install", "@antora/lunr-extension"]

    inputs.files("${project.ext.nodeProjectDir}/package.json")
    outputs.dir("${project.ext.nodeProjectDir}/node_modules/@antora/lunr-extension")
}

task downloadAntora {
    group = 'Build Dependency Download'
    description "Download all Antora build dependencies for site generation."

    dependsOn tasks.downloadAntoraCli
    dependsOn tasks.downloadAntoraSiteGenerator
    dependsOn tasks.downloadAntoraLunrExtension
}

task downloadLinkValidator(type: NpmTask) {
    group = 'Build Dependency Download'
    args = ["install", "link-checker"]

    inputs.files("${project.ext.nodeProjectDir}/package.json")
    outputs.dir("${project.ext.nodeProjectDir}/node_modules/link-checker")
}

task downloadDefaultUITemplate(type: NpmTask) {
    group = 'Build Dependency Download'
    args = ["install", "gitlab:antora/antora-ui-default", "--include=dev"]

    inputs.files("${project.ext.nodeProjectDir}/package.json")
    outputs.dir("${project.ext.nodeProjectDir}/node_modules/@antora/ui-default")
}

task startUIBuildDir(type: Copy) {
    group = 'Site - UI'
    dependsOn tasks.downloadDefaultUITemplate

    // Copy the antora latest default ui into the root folder, to mimick a new checkout of the default ui
    with {
        from "${project.ext.nodeProjectDir}/node_modules/@antora/ui-default"
        duplicatesStrategy = 'EXCLUDE'
    }

    // Overwrite the parts of the UI that we have changed.
    from("${projectDir}/ui-src") {
        into "src"
    }
    duplicatesStrategy = 'INCLUDE'

    into project.ext.uiBuildDir
}

task setupUIBuildDir(type: NpmTask) {
    group = 'Site - UI'
    dependsOn tasks.startUIBuildDir
    workingDir = file(project.ext.uiBuildDir)
    args = ["install"]

    inputs.files("${project.ext.uiBuildDir}/package.json")
    outputs.dir("${project.ext.uiBuildDir}/node_modules/")
}

// Note that Gulp must be installed in the UI Build Directory, not our project node_modules
task downloadGulp(type: NpmTask) {
    group = 'Build Dependency Download'
    dependsOn tasks.startUIBuildDir

    args = ["install", "gulp-cli", "--legacy-peer-deps"]
    execOverrides {
        // The it variable contains the `ExecSpec`
        workingDir = project.ext.uiBuildDir
    }

    inputs.property("Antora version", project.ext.antoraVersion)
    inputs.files("${project.ext.uiBuildDir}/package.json")
    outputs.dir("${project.ext.uiBuildDir}/node_modules/")
}

/*
    Build and/or validate the Site
 */

task buildUIBundle(type: NpxTask) {
    group = 'Site - UI'
    description "Creates a local UI bundle using the 'ui-src' directory. This can be used to preview UI changes with a local or official ref-guide build."

    dependsOn tasks.setupUIBuildDir
    dependsOn tasks.downloadGulp

    command = "gulp"

    args = [
        "bundle",
    ]
    execOverrides {
        // The it variable contains the `ExecSpec`
        workingDir = project.ext.uiBuildDir
    }

    doLast {
        copy {
            from "${project.ext.uiBuildDir}/build/ui-bundle.zip"
            into "${buildDir}/dist"
        }
    }

    inputs.files(fileTree("${project.ext.uiBuildDir}/src"))
    outputs.file("${buildDir}/dist/ui-bundle.zip")

    doLast {
        project.logger.lifecycle("The generated ui-bundle.zip can be found at:")
        project.logger.lifecycle("\t${buildDir}/dist/ui-bundle.zip")
    }
}

task setupLocalSiteStagingDir(type: Sync) {
    group = 'Site - Local'
    description "Creates a staging directory to build the local ref-guide."

    dependsOn tasks.buildLocalAntoraYaml
    dependsOn configurations.localPlaybook

    from(projectDir) {
        include "modules/**"
    }
    from project.ext.localAntoraYaml
    from(project.ext.playbooksDir) {
        include project.ext.localPlaybook
        rename {
            "playbook.yml"
        }
    }

    into project.ext.siteStagingDir
}

task buildLocalSite(type: NpxTask) {
    group = 'Site - Local'
    description "Creates a local build of the ref-guide."

    dependsOn tasks.downloadAntora
    dependsOn tasks.setupLocalSiteStagingDir

    command = "antora"

    // Use a local build of the Lucene and Solr javadocs for a local refGuide
    def extraArgs = [
        // This attribute should not include ":link", the other 2 should
        // This attribute should also not be relative, because it is used in multiple path directories in the site
        "--attribute", "page-solr-javadocs=${project(':solr:documentation').docroot.toPath()}",
        "--attribute", "solr-javadocs=link:../../../${file(project.ext.siteDir).relativePath(project(':solr:documentation').docroot)}",
        "--attribute", "lucene-javadocs=link:../../../${file(project.ext.siteDir).relativePath(project(':solr:documentation').luceneDocsDir)}"
    ]

    if (buildLocalUI) {
        dependsOn tasks.buildUIBundle
        extraArgs.addAll([
            "--ui-bundle-url", "${buildDir}/dist/ui-bundle.zip"
        ])
        inputs.files("${buildDir}/dist/ui-bundle.zip")
    }

    args = [
        "${project.ext.siteStagingDir}/playbook.yml",
    ] + extraArgs

    environment = ["SITE_SEARCH_ENABLED": "true"]

    inputs.files(fileTree(project.ext.siteStagingDir))
    inputs.property("Antora version", project.ext.antoraVersion)
    outputs.dir(project.ext.siteDir)

    doLast {
        project.logger.lifecycle("The generated local ref-guide can be found at:")
        project.logger.lifecycle("\t${project.ext.siteDir}")
    }
}

task checkSiteLinks(type: NpxTask) {
    group = 'Verification'
    description "Check that all links in the ref-guide, both internal and java-docs, are correct."

    dependsOn tasks.downloadLinkValidator
    dependsOn tasks.buildLocalSite

    command = "link-checker"
    args = [
        "${buildDir}/site",
        "--url-ignore", "file:/",
        "--mkdocs",
        "--allow-hash-href",
        "--disable-external",
        "--debug",
        // Ignore the java docs url in the header, since it is an absolute path
        "--url-ignore", "${project(':solr:documentation').docroot.toPath()}/index\\.html",
        "--json",
        "-v",
    ]

    def outputFile = "${buildDir}/validation/link-check.json"
    execOverrides {
        // The it variable contains the `ExecSpec`
        it.standardOutput = new TeeOutputStream(it.standardOutput, new FileOutputStream(outputFile))
    }

    inputs.files(fileTree(siteDir))
    outputs.file(outputFile)
}

/*
    Build and setup the official Site
 */
task setOfficialAntoraYaml {
    group = 'Site - Official'
    description "Setups the local antora.yml as the official antora.yml for this branch. Note: This will change the antora.yml in the repo, not a build folder."

    dependsOn tasks.buildLocalAntoraYaml

    inputs.file(project.ext.localAntoraYaml)
    outputs.file("antora.yml")

    doLast {
        copy {
            from project.ext.localAntoraYaml
            into projectDir
        }
    }
}

task buildOfficialSite(type: NpxTask) {
    group = 'Site - Official'
    description "Creates a build of the official ref-guide. This will not include local changes."

    dependsOn tasks.downloadAntora
    dependsOn configurations.officialPlaybook

    command = "antora"

    def officialPlaybook = "${project.ext.playbooksDir}/${project.ext.officialPlaybook}"
    def extraArgs = []

    if (buildLocalUI) {
        dependsOn tasks.buildUIBundle
        extraArgs.addAll([
            "--ui-bundle-url", "${buildDir}/dist/ui-bundle.zip"
        ])
        inputs.files("${buildDir}/dist/ui-bundle.zip")
    }

    args = [
        officialPlaybook,
    ] + extraArgs

    environment = ["SITE_SEARCH_ENABLED": "true"]

    inputs.files(officialPlaybook)
    inputs.property("Antora version", project.ext.antoraVersion)
    outputs.dir(project.ext.siteDir)

    doLast {
        project.logger.lifecycle("The generated official ref-guide can be found at:")
        project.logger.lifecycle("\t${project.ext.siteDir}")
    }
}
