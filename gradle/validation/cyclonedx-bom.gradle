/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// This adds CycloneDX BOM generation of project dependencies

// If -Pvalidation.cyclonedx-bom=true is set the CycloneDX BOM generation will also run as part of the check task.


allprojects { prj ->
  plugins.withType(JavaPlugin) {
    prj.apply plugin: 'org.cyclonedx.bom'

    ext {
      sbomDir = layout.buildDirectory.dir("sbom")
    }

    configurations {
      sbom
    }

    def baseName = prj.archivesBaseName + "-" + version + "-cyclonedx-bom"

    cyclonedxBom {
      includeConfigs = ["runtimeClasspath"]
      skipConfigs = ["compileClasspath", "testCompileClasspath"]
      projectType = "library"
      destination = sbomDir.map(Directory::getAsFile)
      outputName = baseName
      outputFormat = "all"
      includeBomSerialNumber = true
      componentVersion = version
    }
    assemble.dependsOn cyclonedxBom

    ext {
      sbomXmlArtifact = artifacts.add('sbom', sbomDir.map(d -> d.file(baseName + ".xml"))) {
        type 'cyclonedx-bom'
        extension = 'xml'
        classifier = 'cyclonedx-bom'
        builtBy cyclonedxBom
      }
      sbomJsonArtifact = artifacts.add('sbom', sbomDir.map(d -> d.file(baseName + ".json"))) {
        type 'cyclonedx-bom'
        extension = 'json'
        classifier = 'cyclonedx-bom'
        builtBy cyclonedxBom
      }
    }
  }
}