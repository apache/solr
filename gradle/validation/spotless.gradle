/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/*
 * SOLR-14920 modelled on LUCENE-9564: This adds automatic (and enforced) code formatting using
 * spotless and Google Java Format.
 */

def resources = scriptResources(buildscript)

configure(project(":solr").subprojects) { prj ->
  plugins.withType(JavaPlugin) {
    prj.apply plugin: 'com.diffplug.spotless'

    spotless {
      java {
        toggleOffOn() // obviously, only to be used sparingly.
        // TODO: Work out how to support multiple different header files (we have
        // classes in the codebase that have original headers). We currently use
        // Apache RAT to enforce headers so this is of lesser priority.
        //
        // licenseHeaderFile file("${resources}/asl-header.txt"), '^(\\s*package)'

        lineEndings 'UNIX'
        endWithNewline()
        googleJavaFormat('1.11.0')

        // Apply to all Java sources
        target "src/**/*.java"

        // Exclude certain files (generated ones, mostly).
        switch (project.path) {
          case ":solr:modules:analytics":
          case ":solr:modules:analysis-extras":
          case ":solr:modules:clustering":
          case ":solr:modules:extraction":
          case ":solr:modules:gcs-repository":
          case ":solr:modules:hdfs":
          case ":solr:modules:langid":
          case ":solr:modules:ltr":
          case ":solr:modules:scripting":
          case ":solr:prometheus-exporter":
          case ":solr:core":
          case ":solr:solrj":
          case ":solr:solr-ref-guide":
          case ":solr:test-framework":
            targetExclude "src/**/*.java"
            break
        }
      }
    }

    // Workaround for an odd problem in spotless where it fails because
    // of a missing folder.
    spotlessJava {
      doFirst {
        project.mkdir("${buildDir}/spotless/spotlessJava")
      }
    }
  }

  // Add an alias to 'spotlessApply' simply called 'tidy' and wire up
  // spotlessCheck to convention's check.
  task tidy() {
    description "Applies formatters and cleanups to sources."
    group "verification"
  }

  tasks.matching { task -> task.name == "spotlessApply" }.configureEach { v ->
    tidy.dependsOn v
  }

  tasks.matching { task -> task.name == "spotlessCheck" }.configureEach { v ->
    check.dependsOn v
  }
}
