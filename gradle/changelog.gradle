/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

logchange {
  rootPath = "."
  inputDir = "changelog"
  outputFile = "CHANGELOG.md"
  generateChangesXml = false
}

task writeChangelog {
  description = 'Generates a change/log description file (YAML)'
  doLast {
    def gitBranchFull = 'git rev-parse --abbrev-ref HEAD'.execute().text.trim()

    // Prevent running on main or branches prefixed with 'branch_'
    if (gitBranchFull == 'main' || gitBranchFull.startsWith('branch_')) {
      throw new GradleException("Cannot create changelog on branch '${gitBranchFull}'. This task should only be run on feature branches.")
    }

    def gitBranch = gitBranchFull.replaceFirst(/^.*\//, "")

    def gitUserName = 'git config user.name'.execute().text.trim()
    def configuredName = providers.gradleProperty("user.name").getOrElse(gitUserName)
    def githubId = providers.gradleProperty("user.githubid").getOrElse(null)
    def nick = githubId ? "\n    nick: ${githubId}" : ""
    def asfId = providers.gradleProperty("user.asfid").getOrElse(null)
    def asfIdUrl = asfId ? "\n    url: https://home.apache.org/phonebook.html?uid=${asfId}" : ""
    def jiraMatcher = gitBranch =~ /(?i)SOLR-\d\d\d+/
    def jiraRef = jiraMatcher ? jiraMatcher[0].toUpperCase() : "SOLR-XXXX"
    def jiraUrl = "https://issues.apache.org/jira/browse/${jiraRef}"
    def jiraLinks = jiraMatcher ? "links:\n  - name: ${jiraRef}\n    url: ${jiraUrl}" : ""
    def title = gitBranch.replaceFirst(/(?i)SOLR-\d\d\d+\b-?/, "").replace("-", " ").capitalize()
    // Warn user when generating empty title
    if (title.isEmpty()) {
      println "WARNING: Title in generated changelog is empty, please edit"
    }
    // Strip everything before and including '/', then sanitize special characters
    def sanitizedBranchName = gitBranch.replaceAll(/^.*\//, "").replaceAll(/[^a-zA-Z0-9._-]/, "-")
    def fileName = "changelog/unreleased/${sanitizedBranchName}.yml"
    def file = new File(fileName)
    file.parentFile.mkdirs()
    file.text = """# (DELETE ALL COMMENTS UP HERE AFTER FILLING THIS IN

# See https://github.com/apache/solr/blob/main/dev-docs/changelog.adoc

# If the change is minor, don't bother adding a changelog entry.
# For `other` type entries, the threshold to bother with a changelog entry should be even higher.

# title:
#  * The audience is end-users and administrators, not committers.
#  * Be short and focused on the user impact.  Multiple sentences is fine!
#  * For technical/geeky details, prefer the commit message instead of changelog.
#  * Reference JIRA issues like `SOLR-12345`, or if no JIRA but have a GitHub PR then `PR#12345`.

# type:
#  `added` for new features/improvements, opt-in by the user typically documented in the ref guide
#  `changed` for improvements; not opt-in
#  `fixed` for improvements that are deemed to have fixed buggy behavior
#  `deprecated` for marking things deprecated
#  `removed` for code removed
#  `dependency_update` for updates to dependencies
#  `other` for anything else, like large/significant refactorings, build changes,
#    test infrastructure, or documentation.
#    Most such changes are too small/minor to bother with a changelog entry.

title: ${title}
type:
authors:
  - name: ${configuredName}${nick}${asfIdUrl}
${jiraLinks}
"""

    println "Generated file: ${fileName} -- open and edit before committing"
    println "Read dev-docs/changelog.adoc if you don't contribute here often."
  }
}

task changelog {
  dependsOn writeChangelog
  description = 'Generates a change/log description file (YAML)'
}

task newChangelog {
  dependsOn writeChangelog
  description = 'Generates a change/log description file (YAML)'
}
